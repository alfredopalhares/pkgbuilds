# Maintainer: Alfredo Palhares <masterkorp@masterkorp.net>
pkgname=freescale-s12x-binutils-xgate-git
pkgver=20130410
pkgrel=1
pkgdesc="Binary utilities for the S12X and XGATE processors by Freescale - Patched by Sean Keys"
arch=("i686" "x86_64")
url="https://github.com/seank/FreeScale-s12x-binutils"
license=('GPL')
groups=("freescale-s12x-toolchain-xgate")
depends=('glibc' 'zlib')
makedepends=('git')
options=('!libtool' '!distcc' '!ccache')
install=

_gitroot=git://github.com/seank/FreeScale-s12x-binutils.git
_gitname=FreeScale-s12x-binutils

pkgver() {
  cd "$srcdir/$_gitname"
  git describe --always | sed 's/-/./g'
}

build() {
  cd "$srcdir"
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin
    msg "The local files are updated."
  else
    git clone  "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$srcdir/$_gitname-build"
  git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build"
  cd "$srcdir/$_gitname-build"

  #
  # BUILD HERE
  #
  ./configure --target=xgate \
              --enable-targets=m68hc11,m68hc12,xgate \
              --prefix=/usr --with-lib-prefix=xgate\
              --with-lib-path=/usr/lib/xgate --enable-ld=default \
              --enable-plugins --enable-shared \
              --program-prefix=xgate- \
              --disable-werror --enable-multilib

  # Check host
  make configure-host

  make tooldir="/usr"
}

check() {
  cd "$srcdir/$_gitname-build"

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check
}

package() {
  cd "$srcdir/$_gitname-build"

  make prefix="$pkgdir/usr" tooldir="$pkgdir/usr" install

  # Remove stuff to not collide with existent binutils
  rm "$pkgdir/usr/bin/"{ar,as,nm,objcopy,objdump,ranlib,strip}
}

# vim:set ts=2 sw=2 et:
