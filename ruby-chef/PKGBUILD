# Maintainer: Alfredo Palhares <masterkorp@masterkorp.net>
# Please submit pactches and updates to:
# https://github.com/masterkorp/pkgbuilds

_gemname=chef
pkgname=ruby-$_gemname
pkgver=11.6.2
pkgrel=2
pkgdesc="A systems integration framework, built to bring the benefits of configuration management to your entire infrastructure."
arch=(any)
url="http://wiki.opscode.com/display/chef"
license=('GPL')
depends=('ruby'
  'ruby-bunny>0.6.0'
  'ruby-erubis'
  'ruby-highline'
  'ruby-mixlib-log>=1.3.0'
  'ruby-mixlib-authentication>=1.1.0'
  'ruby-mixlib-cli>=1.3.0'
  'ruby-mixlib-config-1.1.2'
  'ruby-mixlib-shellout-1.2'
  'ruby-moneta'
  'ruby-net-ssh-2.2'
  'ruby-net-ssh-multi-1.1.2'
  'ruby-ohai>=0.6.0'
  'ruby-rest-client<1.7.0'
  'ruby-treetop>=1.4.9'
  'ruby-uuidtools'
  'ruby-yajl-ruby'
  )
makedepends=(rubygems)
source=(http://gems.rubyforge.org/gems/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
md5sums=('8db9eb4b3c75be30e8125ea4eedd3f01')


package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" \
    "$_gemname-$pkgver.gem"
}

# vim:set ts=2 sw=2 et:
