# Maintainer: Alfredo Palhares <masterkorp@masterkorp.net>
# Please submit pactches and updates to:
# https://github.com/masterkorp/pkgbuilds

_gemname=chef-zero
pkgname=ruby-$_gemname
pkgver=1.7.2
pkgrel=1
pkgdesc="Self-contained, easy-setup, fast-start in-memory Chef server for testing and solo setup purposes."
arch=(any)
url="http://wiki.opscode.com/display/chef"
license=('GPL')
depends=('ruby'
  'ruby-hashie'
  'ruby-json'
  'ruby-mixlib-log>=1.3.0'
  'ruby-moneta'
  )
makedepends=(rubygems)
source=(http://gems.rubyforge.org/gems/$_gemname-$pkgver.gem)
noextract=($_gemname-$pkgver.gem)
md5sums=('884fb6ca2e75ae515ffec97f1fc77793')

package() {
  cd "$srcdir"
  # _gemdir is defined inside package() because if ruby[gems] is not installed on
  # the system, makepkg will exit with an error when sourcing the PKGBUILD.
  local _gemdir="$(ruby -rubygems -e'puts Gem.default_dir')"

  gem install --ignore-dependencies --no-user-install -i "$pkgdir$_gemdir" -n "$pkgdir/usr/bin" \
    "$_gemname-$pkgver.gem"
}

# vim:set ts=2 sw=2 et:
