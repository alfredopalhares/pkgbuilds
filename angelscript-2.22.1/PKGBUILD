# Maintainer: Alfredo Palhares <masterkorp@masterkorp.net>

pkgname=angelscript-2.22.1
pkgver=2.22.1
pkgrel=3
pkgdesc="An extremely flexible cross-platform scripting library. - Specific version for Rigs of Rods"
arch=('i686' 'x86_64')
url="http://www.angelcode.com/angelscript/"
license=('custom:ZLIB')
depends=('gcc-libs')
conflicts=("angelscript" "angelscript2.10")
source=("http://www.angelcode.com/angelscript/sdk/files/angelscript_2.22.1.zip"
        LICENSE)
md5sums=('bc238a09ad08c1d075660f4a87f2d738'
         '7acd33f0f7a5d7c71233195fe502f6c0')

build() {
  cd "$srcdir/sdk/angelscript/projects/gnuc"

  sed -i -e "/^LOCAL/s:=.*:= ${pkgdir}/usr:" \
    -e '/\sCXXFLAGS/s:-g::' \
    -e '/^install/s/:.*/:/' \
    -e 's/ln -s/ln -fs/' \
    makefile

  make
  make SHARED=1 VERSION=$pkgver
}

package() {
  cd "$srcdir/sdk/angelscript/projects/gnuc"

  install -d "$pkgdir"/usr/{lib,include} \
             "$pkgdir"/usr/share/{licenses,doc}/$pkgname \
             "$pkgdir"/usr/share/$pkgname \
             "$pkgdir"/usr/share/doc/$pkgname/{articles,manual}

  make install
  make install SHARED=1 VERSION=$pkgver

  chmod 644 "$pkgdir"/usr/include/*

  msg "Installing add-ons..."
  cd "$srcdir/sdk"
  cp -rf add_on "$pkgdir/usr/share/$pkgname/"

  msg "Installing documentation and license..."
  cd "$srcdir/sdk/docs"
  install -m644 articles/* "$pkgdir/usr/share/doc/$pkgname/articles"
  install -m644 manual/* "$pkgdir/usr/share/doc/$pkgname/manual"
  install -m644 index.html "$pkgdir/usr/share/doc/$pkgname"
  install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname"
}
